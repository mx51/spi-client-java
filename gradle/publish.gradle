// See: https://central.sonatype.org/publish/publish-gradle/

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.diffplug.osgi.bndmanifest'

jar.manifest.attributes(
        'Bundle-Description': project.publishName,
        'Bundle-Vendor': project.publishVendor,
        'Implementation-Title': project.publishName,
        'Implementation-Vendor': project.publishVendor,
        'Implementation-Version': project.version
)

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

if (hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar

                groupId project.group
                artifactId project.archivesBaseName
                version project.version

                pom {
                    name = project.publishName
                    description = project.publishDescription
                    url = project.publishScmUrl

                    licenses {
                        license {
                            name = project.publishLicenseName
                            url = project.publishLicenseUrl
                        }
                    }

                    developers {
                        developer {
                            id = project.publishDeveloperId
                            name = project.publishDeveloperName
                        }
                    }

                    scm {
                        url = project.publishScmUrl
                        connection = project.publishScmConnection
                        developerConnection = project.publishScmDeveloperConnection
                    }
                }
            }
        }

        repositories {
            maven {
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username project['ossrhUsername']
                    password project['ossrhPassword']
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    publish.dependsOn {
        if (!rootProject.hasProperty('ossrhUsername') || !rootProject.hasProperty('ossrhPassword')) {
            throw new IllegalArgumentException("Properties 'ossrhUsername' and 'ossrhPassword'" +
                    " must be defined to access the Sonatype repository!")
        }
    }
}
